buildscript {
    repositories {
        maven { url = "https://jcenter.bintray.com/" }
        maven { url = "https://repo1.maven.org/maven2/" }
        maven { url = "https://maven.google.com/" }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
        classpath 'com.guardsquare:proguard-gradle:7.0.1'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

// Reproducible builds! https://docs.gradle.org/4.9/userguide/working_with_files.html#sec:reproducible_archives
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

apply plugin: 'java'
apply plugin: 'forge'

// To use Java toolchains, sourceCompatibility & targetCompatibility need to be not set.
// ForgeGradle sets these previously to build some Forge internals, as it targets Gradle 4.X and above.
// This workaround sets them back to null. As this is a hack, any issues caused by this are my fault.
sourceCompatibility = null
targetCompatibility = null

// This Java toolchain builds this mod with a Java 6 compatible JDK.
def compiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(6)
}

java {
    toolchain {
        compiler
    }
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

version = project.mod_version
group = project.mod_group

def modName = project.mod_name
def build_release = System.getenv("BUILD_RELEASE") == "true"

// Add snapshot suffix to version if not building a release.
// To build a release, set the environment variable BUILD_RELEASE to true.
if (!build_release) {
    version += "-SNAPSHOT"
}

// Information for ForgeGradle to configure the Minecraft / Forge version.
minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modName", project.mod_name

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        expand 'version': version, 'modName': modName, 'mcversion': project.minecraft.version
    }

    // Here's a super unnecessary micro-optimisation: a mcmod.info file is a JSON file, and JSON files can be minified!
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/mcmod.info").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

tasks.withType(JavaCompile) {
    // Ensures that the encoding of source files is set to UTF-8, see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    options.encoding = "UTF-8"
    // Tells Gradle not to include debug information in the compiled .class files. This reduces the file size.
    options.debug = false
}

// This task creates a .jar file containing the javadoc for the code of this mod.
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier "javadoc"
    from javadoc.outputs
}

// This task creates a .jar file containing the source code of this mod.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

// This task creates a .jar file containing a deobfuscated version of this mod, for other developers to use in a development environment.
task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
}

task ('proguard', type: proguard.gradle.ProGuardTask) {
    // Workaround for ProGuard seemingly not recognising that when the output .jar file doesn't exist
    outputs.upToDateWhen { false }

    verbose

    injars "build/libs/" + rootProject.name + "-" + version + "-dev.jar"
    outjars "build/libs/" + rootProject.name + "-" + version + "-dev-proguard.jar"

    // Get the location of rt.jar from the provided toolchain.
    // Note to self: probably support building with not outdated versions of Java in the future.
    libraryjars compiler.get().metadata.installationPath.asFile.absolutePath + "/lib/rt.jar"
    // Dumb MacOS workaround, rt.jar is not in the standard place
    libraryjars compiler.get().metadata.installationPath.asFile.absolutePath + "/../Classes/classes.jar"

    libraryjars project.configurations.minecraft
    //libraryjars project.configurations.userDevPackageDepConfig
    libraryjars project.configurations.minecraftDeps

    // The rest of the configs are in this file
    configuration 'guard.pro'
}

def optimisedJar = artifacts.add('archives', file(proguard.outputs.files.singleFile)) {
    type('Jar')
    builtBy(proguard)
    classifier = "proguard"
}

task remapProguardProperly(dependsOn: 'genSrgs', type: net.minecraftforge.gradle.tasks.user.reobf.ReobfTask) {
    dependsOn 'proguard'
    def forgeGradlePlugin = plugins.findPlugin 'forge'
    exceptorCfg = forgeGradlePlugin.delayedFile net.minecraftforge.gradle.user.UserConstants.EXC_SRG
    srg = forgeGradlePlugin.delayedFile net.minecraftforge.gradle.user.UserConstants.REOBF_SRG
    fieldCsv = forgeGradlePlugin.delayedFile net.minecraftforge.gradle.user.UserConstants.FIELD_CSV
    fieldCsv = forgeGradlePlugin.delayedFile net.minecraftforge.gradle.user.UserConstants.METHOD_CSV
    mcVersion = forgeGradlePlugin.delayedString '{MC_VERSION}'
    reobf(optimisedJar) { arg ->
        def javaConv = project.convention.plugins.get 'java'
        arg.classpath = javaConv.getSourceSets().getByName('main').compileClasspath
    }
    extraSrg = forgeGradlePlugin.extension.srgExtra
    afterEvaluate {
        if (forgeGradlePlugin.extension.decomp) {
            deobfFile = tasks.deobfuscateJar.delayedOutput
            recompFile = forgeGradlePlugin.delayedDirtyFile forgeGradlePlugin.srcDepName, null, 'jar'
        }
    }
}

// Creates the listed artifacts on building the mod.
artifacts {
    archives javadocJar
    archives sourcesJar
    archives devJar
    archives optimisedJar
}

reobf.dependsOn 'remapProguardProperly'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                builtBy build
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
            artifact(devJar) {
                builtBy devJar
            }
            artifact(javadocJar) {
                builtBy javadocJar
            }
            artifact optimisedJar
        }
    }
}
