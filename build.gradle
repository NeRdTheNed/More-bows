buildscript {
    repositories {
        maven { url = "https://jcenter.bintray.com/" }
        maven { url = "https://repo1.maven.org/maven2/" }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'com.anatawa12.forge:ForgeGradle:1.2-1.0.+'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

// Building for Java 6 should speed up class loading times a tiny amount, due to preverifying classes.
sourceCompatibility = 1.5
targetCompatibility = 1.6

apply plugin: 'java'
apply plugin: 'forge'

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

version = project.mod_version
group = project.mod_group
archivesBaseName = project.mod_basename

def modName = project.mod_name
def build_release = System.getenv("BUILD_RELEASE") == "true"

// Add snapshot suffix to version if not building a release.
// To build a release, set the environment variable BUILD_RELEASE to true.
if (!build_release) {
    version += ("-SNAPSHOT+" + getTimestampSuffix())
}

// Information for ForgeGradle to configure the Minecraft / Forge version.
minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
}

// Creates a timestamp to be appended to the end of the build.
def getTimestampSuffix() {
    def date = new Date()
    return date.format('yyyy-MM-dd_HHmmss')
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modName", project.mod_name
    inputs.property "archivesBaseName", project.mod_basename

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        expand 'version': version, 'mcversion': project.minecraft.version, 'modName': modName, 'archivesBaseName': archivesBaseName
    }

    // Move all access transformers to META-INF.
    rename '(.+_at.cfg)', 'META-INF/$1'

    // Here's a hilarious micro-optimisation for you: mcmod.info is a JSON file, and JSON files can be minified!
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/mcmod.info").each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

// Hack to add variables to source code on build. TODO: Remove this
task processSource(type: Sync) {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modName", project.mod_name
    inputs.property "archivesBaseName", project.mod_basename

    from(sourceSets.main.java) {
        filesMatching('iDiamondhunter/morebowsmod/MoreBowsMod.java') {
            expand 'version': version, 'mcversion': project.minecraft.version, 'modName': modName, 'archivesBaseName': archivesBaseName
        }
    }

    into "$buildDir/src"
}

// Ensures that the encoding of source files is set to UTF-8, see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Use the processed source with replaced build variables. TODO: Remove this.
compileJava {
    source = processSource.outputs
}

// Adds the access transformer to the manifest and names the .jar.
jar {
    baseName = "${project.mod_basename}"

    manifest {
        attributes 'FMLAT': 'iDiamondhunterMoreBows_at.cfg'
    }
}

// This task creates a .jar file containing the javadoc for the code of this mod.
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier "javadoc"
    from javadoc.outputs
}

// This task creates a .jar file containing the source code of this mod.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    //from processResources.outputs
    from processSource.outputs // TODO: Remove this
}

// This task creates a .jar file containing a deobfuscated version of this mod, for other developers to use in a development environment.
task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
}

// Creates the listed artifacts on building the mod.
artifacts {
    archives javadocJar
    archives sourcesJar
    archives devJar
}

// This block configures any maven publications you want to make.
publishing {
    publications {
        mavenJava(MavenPublication) {
            // Add any other artifacts here that you would like to publish!
            artifact(jar) {
                builtBy build
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
            artifact(devJar) {
                builtBy devJar
            }
            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }

    // This block selects the repositories you want to publish to.
    repositories {
        // Add the repositories you want to publish to here.
    }
}
